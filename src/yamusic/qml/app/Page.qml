import QtQuick
import QtQuick.Layouts
import Md3
import YaMusic.Core

// Базовая страница: плавная прокрутка, крупный заголовок, отступ снизу под плавающий мини-плеер.
// Содержимое кладётся прямо внутрь: Page { title: "…"; Label { … } } — оно попадает в ColumnLayout.
Item {
    id: page

    property var router
    property string title
    property string subtitle
    property bool showHeader: true
    property real sideMargin: 32
    property real bottomInset: 120
    default property alias content: column.data
    readonly property alias flickable: flick
    readonly property bool scrolled: flick.contentY > 48

    SmoothFlickable {
        id: flick
        anchors.fill: parent
        contentHeight: column.implicitHeight + 8 + page.bottomInset
        wheelStep: Settings.wheelStep

        ColumnLayout {
            id: column
            x: page.sideMargin
            y: 8
            width: flick.width - 2 * page.sideMargin
            spacing: 24

            ColumnLayout {
                visible: page.showHeader
                Layout.fillWidth: true
                spacing: 4
                Label {
                    Layout.fillWidth: true
                    text: page.title
                    type: "displaySmall"
                    weight: 600
                }
                Label {
                    visible: page.subtitle !== ""
                    Layout.fillWidth: true
                    text: page.subtitle
                    type: "bodyLarge"
                    color: Theme.fgSurfaceVariant
                }
            }
        }
    }
}
